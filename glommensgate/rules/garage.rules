import org.openhab.core.library.types.*
import org.openhab.core.persistence.*
import org.openhab.model.script.actions.*

var Timer garageDoorOpen = null

rule "Send a message if garage door has been open for 30 minutes"
	when
		Item garage_door_1 changed to OPEN or
		Item garage_door_2 changed to OPEN	
	then
		logInfo("org.openhab.rules","Garage door opened")
		sendCommand(message_received, OFF)
		garageDoorOpen = createTimer(now.plusSeconds(1800)) [|
			var t = ""
			if (garage_door_1.state == OPEN) t = "Leafs "
			else t = "Mondeos" 
			logInfo("org.openhab.rules", t+ " Garage door has been open too long")
			postUpdate(push_message_text, t + " garasjeport er ikke lukket")
			
			if(garageDoorOpen!=null) {
				if(garageDoorOpen.running) {
					garageDoorOpen.cancel()
				}
				garageDoorOpen = null
			}
		]
end

rule "Reset timer if someone closes the garage door, and both is closed"
	when
		Item garage_door_1 changed to CLOSED or
		Item garage_door_2 changed to CLOSED
	then
		logInfo("org.openhab.rules", "Garage door closed")
		sendCommand(message_received, OFF)
		if (garage_door_1.state == CLOSED && garage_door_2.state == CLOSED) {
			if(garageDoorOpen!=null) {
			garageDoorOpen.cancel
			garageDoorOpen = null
			}
		} else {
			garageDoorOpen.reschedule(now.plusSeconds(1200))
		}	
end

rule "Send a message after 10 minutes if garage door is open and nobody is home"
	when
		Time cron "0 0/10 * * * ?"
	then
		if (Home.state == OFF && (garage_door_1.state == OPEN || garage_door_2.state == OPEN)) {
			if (message_received.state == OFF && !garage_door_1.changedSince(now.minusMinutes(15)) && !garage_door_2.changedSince(now.minusMinutes(15))) {
				postUpdate(push_message_text, "Tydeligvis ingen hjemme, men garasjeporten er Ã¥pen!")
			}
		}
		if (garage_door_1.state == Undefined || garage_door_1.state == Uninitialized) {
        	sendCommand(garage_door_1, CLOSED)
        }
        if (garage_door_2.state == Undefined || garage_door_2.state == Uninitialized) {
        	sendCommand(garage_door_2, CLOSED)
        }	
end

rule "Send a status message at 22 if anything is NOT ok"
	when
		Time cron "0 0 22 ? * *"
	then
		if (garage_door_1.state == OPEN || garage_door_2.state == OPEN) {
			postUpdate(push_message_text, "Leaf: " + garage_door_1.state.toString() + ", Mondeo:" + garage_door_2.state.toString())
		}
		if (boss_lan.state == OFF && !boss_lan.changedSince(now.minusHours(12))) {
			pushover('Boss is not responding, and has been off for 12 hours')
		}
		if (nas_lan.state == OFF && !nas_lan.changedSince(now.minusHours(12))) {
			pushover('NAS is not responding, and has been off for 12 hours')
		} 	 	
end